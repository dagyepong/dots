import QtQuick
import QtQuick.Layouts
import IslandBackend

RowLayout {
  id: root

  property real buttonBorderWidth
  property string buttonBorderColor
  property real buttonWidth
  property real buttonHeight
  property real buttonRadius
  property color buttonBgOff
  property color buttonFgOff

  property bool notificationPopup: false
  property bool controlCenterOpen: false
  property bool mediaAutoOpened: false
  property bool wifiPanelOpened: false
  property bool hasPlayer: false
  property real playerHeight: 0

  anchors.top: parent.top
  anchors.topMargin: hasPlayer ? playerHeight + 92 : 5
  anchors.left: parent.left
  anchors.right: parent.right
  anchors.leftMargin: 5
  anchors.rightMargin: 5

  onControlCenterOpenChanged: {
    if (!controlCenterOpen) root.wifiPanelOpened = false
  }

  // wifi
  Rectangle {
    id: wifiBtn
    width: root.buttonWidth
    height: root.buttonHeight
    radius: root.buttonRadius
    visible: root.controlCenterOpen && !root.mediaAutoOpened
    color: WifiController.enabled ? "#212529" : (wifiHover.hovered ? Qt.lighter(root.buttonBgOff, 1.5) : root.buttonBgOff)
    border.width: buttonBorderWidth
    border.color: buttonBorderColor
    scale: wifiMouse.pressed ? 0.93 : 1.0
    Behavior on color { ColorAnimation { duration: 150 } }
    Behavior on scale { NumberAnimation { duration: 80; easing.type: Easing.OutQuad } }

    RowLayout {
      anchors.centerIn: parent
      spacing: 5
      Text {
        text: "\uf1eb" // wifi glyph
        color: WifiController.enabled ? "#3f7de0" : root.buttonFgOff
        font { family: Theme.nerdFontFamily; pixelSize: 14 }
      }
      Text {
        text: !WifiController.enabled ? "Off"
            : WifiController.currentSsid.length > 0 ? WifiController.currentSsid
            : (WifiController.statusText.length > 0 ? WifiController.statusText : "Not connected")
        color: WifiController.enabled ? "#dedede" : root.buttonFgOff
        font { family: Theme.fontFamily; pixelSize: 12; weight: 400 }
        elide: Text.ElideRight
        Layout.maximumWidth: 90
      }
    }

    HoverHandler { id: wifiHover }
    MouseArea {
      id: wifiMouse
      anchors.fill: parent
      acceptedButtons: Qt.LeftButton | Qt.RightButton
      cursorShape: Qt.PointingHandCursor
      onClicked: (mouse) => {
        if (mouse.button === Qt.RightButton) {
          root.wifiPanelOpened = !root.wifiPanelOpened
          if (root.wifiPanelOpened && WifiController.enabled) WifiController.refreshNetworks(true)
          return
        }
        WifiController.setEnabled(!WifiController.enabled)
      }
    }
  }

  WifiPanel {
    visible: root.wifiPanelOpened
    anchorX: wifiBtn.mapToGlobal(0, 0).x - width  // sit left of the wifi button
    anchorY: wifiBtn.mapToGlobal(0, 0).y
  }

  onNotificationPopupChanged: {
    if (root.notificationPopup) root.wifiPanelOpened = false
  }

  Item { Layout.fillWidth: true }

  // silent notifications
  Rectangle {
    id: dndBtn
    width: root.buttonWidth
    height: root.buttonHeight
    radius: root.buttonRadius
    visible: root.controlCenterOpen && !root.mediaAutoOpened
    color: notificationModule.dndEnabled ? "#2e2c28" : (dndHover.hovered ? Qt.lighter(root.buttonBgOff, 1.5) : root.buttonBgOff)
    border.width: buttonBorderWidth
    border.color: buttonBorderColor
    scale: dndMouse.pressed ? 0.93 : 1.0
    Behavior on color { ColorAnimation { duration: 150 } }
    Behavior on scale { NumberAnimation { duration: 80; easing.type: Easing.OutQuad } }

    Text {
      text: String.fromCodePoint(0xf1f6)
      color: notificationModule.dndEnabled ? "#fbf5e8" : root.buttonFgOff
      anchors.centerIn: parent
      font { family: Theme.nerdFontFamily; pixelSize: 14 }
    }
    HoverHandler { id: dndHover }
    MouseArea {
      id: dndMouse
      anchors.fill: parent
      cursorShape: Qt.PointingHandCursor
      onClicked: notificationModule.dndEnabled = !notificationModule.dndEnabled
    }
  }

  Item { Layout.fillWidth: true }

  // timer / countdown
  Rectangle {
    id: timerBtn
    width: root.buttonWidth
    height: root.buttonHeight
    radius: root.buttonRadius
    color: countdownModule.running ? "#25282c" : (timerHover.hovered ? Qt.lighter(root.buttonBgOff, 1.5) : root.buttonBgOff)
    border.width: buttonBorderWidth
    border.color: buttonBorderColor
    scale: timerMouse.pressed ? 0.93 : 1.0
    Behavior on color { ColorAnimation { duration: 150 } }
    Behavior on scale { NumberAnimation { duration: 80; easing.type: Easing.OutQuad } }
    property int selectedMinutes: 1

    RowLayout {
      anchors.centerIn: parent
      spacing: 5
      Text {
        text: {
          if (countdownModule.running) return String.fromCodePoint(0xf1ade)
          if (countdownModule.remainingSeconds > 0) return String.fromCodePoint(0xf1ae0)
          return String.fromCodePoint(0xf13ab)
        }
        color: countdownModule.running ? "#3978c7" : root.buttonFgOff
        font { family: Theme.nerdFontFamily; pixelSize: 14 }
      }
      Text {
        text: countdownModule.running || countdownModule.remainingSeconds > 0
            ? countdownModule.formatted() : timerBtn.selectedMinutes + "m"
        color: countdownModule.running ? "#dedede" : root.buttonFgOff
        font { family: Theme.fontFamily; pixelSize: 12; weight: 400 }
      }
    }

    HoverHandler { id: timerHover }
    MouseArea {
      id: timerMouse
      anchors.fill: parent
      acceptedButtons: Qt.LeftButton | Qt.RightButton | Qt.MiddleButton
      cursorShape: Qt.PointingHandCursor
      onClicked: (mouse) => {
        if (mouse.button === Qt.MiddleButton) { countdownModule.reset(); return }
        if (mouse.button === Qt.RightButton) {
          if (countdownModule.running || countdownModule.remainingSeconds > 0) return
          const presets = Config.timerPresets
          const idx = presets.indexOf(timerBtn.selectedMinutes)
          timerBtn.selectedMinutes = presets[(idx + 1) % presets.length]
          return
        }
        if (countdownModule.running) { countdownModule.pause(); return }
        if (countdownModule.remainingSeconds > 0) { countdownModule.resume(); return }
        countdownModule.start(timerBtn.selectedMinutes)
      }
    }
  }
}
